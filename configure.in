# ------------------------------------------------------------------------------
# Copyright 2002-2010, GridWay Project Leads (GridWay.org)          
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may
# not use this file except in compliance with the License. You may obtain
# a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ------------------------------------------------------------------------------

AC_PREREQ(2.59)
AC_INIT([GridWay],[5.7],[contact@gridway.org],[gridway],[http://gridway.org])
AC_CONFIG_AUX_DIR(config)
AC_CONFIG_SRCDIR([src/dm/gw_dm_action_dispatch.c])

AC_PREFIX_DEFAULT([/opt/AC_PACKAGE_TARNAME/AC_PACKAGE_VERSION])

#-------------------------------------------------------------------------------
# Check target system
#-------------------------------------------------------------------------------

AC_CANONICAL_BUILD

case "$build_os" in
    *linux*)
        system=linux
    ;;
    *solaris*)
        system=solaris
        CFLAGS+="-DGWSOLARIS"
        LDFLAGS+="-lsocket -lnsl"
    ;;
    *darwin*)
        JAVA_HOME=/System/Library/Frameworks/JavaVM.framework/Home
        CFLAGS+="-I/System/Library/Frameworks/JavaVM.framework/Headers" 
    ;;
   
    *osf5.1* | *tru64*)
        system=alpha
        CFLAGS+="-pthread -D_POSIX_PII_SOCKET"
    ;;
    *aix*)
        system=
    ;;
esac

AC_SUBST(system, $system)
AC_SUBST(java_home, $JAVA_HOME)

#------------------------------------------------------------------------------
# Argument Definition for configure 
#------------------------------------------------------------------------------

drmaa="yes"	
drmaa_ruby=""
docs="no"
tests="no"
drmaa1="no"
jsdl="yes"	
db="yes"

# --- Enable DRMAA library ---

AC_ARG_ENABLE(drmaa,  AS_HELP_STRING(--disable-drmaa,\
  Do not build drmaa support. Default is enabled.),\
  [if test $enableval = "yes"; then drmaa="yes"; else drmaa=" "; fi], [])

# --- Enable DRMAA 1.0 JAVA library ---

AC_ARG_ENABLE(drmaa1,  AS_HELP_STRING(--enable-drmaa1,\
  Build drmaa 1.0 specification for the JAVA Bindings. Default is disabled.),\
  [if test $enableval = "yes"; then drmaa1="drmaa1";drmaa="yes"; else drmaa1=" "; drmaa=" "; fi], [])
  
# --- Enable DRMAA RUBY library ---

AC_ARG_ENABLE(drmaa_ruby,  AS_HELP_STRING(--enable-drmaa-ruby,\
  Build ruby drmaa support. Default is disabled.),\
  [if test $enableval = "yes"; then drmaa_ruby="yes"; else drmaa_ruby=" "; fi], [])

# --- Enable JSDL  ---

AC_ARG_ENABLE(jsdl,  AS_HELP_STRING(--enable-jsdl,\
  Build jsdl support. Disabled for GT builds.),\
  [if test $enableval = "yes"; then jsdl="yes"; else jsdl=" "; fi], [])

# --- Enable Debug ---

AC_ARG_ENABLE(debug,  AS_HELP_STRING(--enable-debug,\
  Enable debug info in GW logs. Default is disabled.),\
  [CFLAGS="$CFLAGS -Wall -g -DGWIMDEBUG -DGWJOBDEBUG -DGWHOSTDEBUG -DGWEMDEBUG -DGWDMDEBUG  -DGWTMDEBUG -DGWUSERDEBUG -DGWSCHEDDEBUG";], [])

# --- With DB ---      

AC_ARG_WITH(db,  AS_HELP_STRING(--with-db=<no|path_to_db>,\
       Build db support for accounting. Disabled for GT builds.),\
       [if test $withval = "no";  then  db=" "; else \       
        if test $withval = "yes"; then  db="yes"; db_path=" "; fi; \
        if test $withval != "yes"; then db="yes"; db_path="$withval"; fi; \
        fi], [])        

# --- With GridWay tests ---

AC_ARG_WITH(tests,  AS_HELP_STRING(--with-tests,\
        Build GridWay test suite. Default is disabled.),\
        [if test $withval = "yes"; then tests="yes"; else tests="no"; fi], [])
          
#------------------------------------------------------------------------------
# Argument check
#------------------------------------------------------------------------------

# --- Enable DRMAA library ---

AM_CONDITIONAL(DRMAA, test "x$drmaa" = "xyes")
AM_CONDITIONAL(DRMAA1, test "x$drmaa1" = "xyes")

## Checking the ruby and ruby-dev
AM_CONDITIONAL(DRMAA_RUBY, test "x$drmaa_ruby" = "xyes")
if test "x$drmaa_ruby" = "xyes"; then
 AC_CHECK_PROGS([RUBY],[ruby],[no])
 if [[ "$RUBY" = "no" ]] ; then
  RUBY_ERROR="ruby not found in your system. Install it!"
  RUBY_TEST="no"
  AC_MSG_ERROR([$RUBY_ERROR])
 else
#  AC_CHECK_HEADERS([sys/time.h])
  AC_MSG_CHECKING([whether $RUBY installation includes the ruby-dev package])
## Next is kind of conftest for ruby
  cat >> conftest.rb << EOF
#!/usr/bin/env ruby
begin
    require 'mkmf'
    exit 0
rescue LoadError
    STDERR.puts 'ruby.dev package not included in ruby!'
    exit -1
end
EOF
  $RUBY conftest.rb 2>/dev/null
  if test $? -eq 0; then
    rm conftest.rb
    RUBY_TEST="yes"
    AC_MSG_RESULT([$RUBY_TEST])
  else
    rm conftest.rb
    RUBY_TEST="no"
    AC_MSG_RESULT([$RUBY_TEST])
    RUBY_ERROR="ruby-dev not installed (mkmf not found). Please install it or remove --enable-drmaa-ruby!"
    AC_MSG_ERROR([$RUBY_ERROR])
  fi
  (GW_SRC=$PWD;cd src/drmaa/ruby;
  ruby extconf.rb -g $GW_SRC -r $prefix/lib/ruby;
  echo -e "\n\ndistdir:\n" >> Makefile)
 fi
fi

# --- Enable JSDL  ---

AM_CONDITIONAL(JSDL, test "x$jsdl" = "xjsdl")

# --- With DB ---

if test "x$db" = "xdb" ; then
	if test "x$db_path"="x " ; then
		LDFLAGS="${LDFLAGS} -L/usr/lib";
		DB4_INCLUDE_PATH="-I /usr/include";	
	else
    	LDFLAGS="${LDFLAGS_SAVED} -L${db_path}/lib";
        DB4_INCLUDE_PATH="-I ${db_path}/include";	
	fi
else
	DB4_INCLUDE_PATH=" ";
fi

AM_CONDITIONAL(DB,test "x$db" = "xdb")

# --- With GridWay tests ---

AM_CONDITIONAL(TESTS, test "x$tests" = "xyes")

#------------------------------------------------------------------------------
# Checks for programs.
#------------------------------------------------------------------------------
AC_PROG_AWK
AC_PROG_YACC
AC_PROG_CC
AC_PROG_CXX
AC_PROG_LN_S
AC_PROG_MAKE_SET

AC_PATH_PROG(JAVAC_PATH, javac)
	
if test "x$JAVAC_PATH" = "x"; then
   	AC_MSG_WARN("A Java compiler is needed for GridWay's DRMAA JAVA and JSDL")
fi
    
if test "x$JAVA_HOME" = "x"; then
   	AC_MSG_WARN("JAVA_HOME is not set, some Java versions will not work (eg. Mac OS X)")     
fi

#------------------------------------------------------------------------------
# Automake Directives
#------------------------------------------------------------------------------

AM_INIT_AUTOMAKE
AM_PROG_CC_C_O
AM_PROG_LEX

#------------------------------------------------------------------------------
# Checks for libraries
#------------------------------------------------------------------------------

AC_CHECK_LIB([pthread], [pthread_cond_wait])

if test "x$db" = "xdb"; then
AC_CHECK_LIB([db], [db_env_create])
fi


#------------------------------------------------------------------------------
# Checks for header files.
#------------------------------------------------------------------------------

AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
# Even if it is suggested by autoscan, it is better to leave malloc.h off
AC_CHECK_HEADERS([arpa/inet.h fcntl.h inttypes.h libintl.h limits.h netdb.h netinet/in.h stddef.h stdlib.h string.h sys/socket.h unistd.h float.h malloc.h])
#AC_CHECK_HEADERS([arpa/inet.h fcntl.h inttypes.h libintl.h limits.h netdb.h netinet/in.h stddef.h stdlib.h string.h sys/socket.h unistd.h])

#------------------------------------------------------------------------------
# Checks for typedefs, structures, and compiler characteristics.
#------------------------------------------------------------------------------

AC_C_CONST
AC_C_INLINE
AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

#------------------------------------------------------------------------------
# Checks for library functions.
#------------------------------------------------------------------------------

AC_REPLACE_FNMATCH
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_FUNC_SELECT_ARGTYPES
AC_FUNC_STAT
AC_FUNC_VPRINTF
AC_FUNC_CLOSEDIR_VOID
AC_CHECK_FUNCS([dup2 getcwd gethostbyname gethostname gettimeofday memset realpath select socket strcasecmp strchr strdup strerror strrchr strstr rmdir setenv])

# Libtool support
AC_PROG_LIBTOOL

#------------------------------------------------------------------------------
# Config Files
#------------------------------------------------------------------------------

AC_CONFIG_FILES([Makefile src/Makefile])
                  
AC_CONFIG_FILES([pkgdata/pkg_data_src.gpt])
AC_OUTPUT
